#!/usr/bin/perl

# ====================================================================
# Sim_Upgrades.pl
# Author: Adam Wills
# Date: Jun 2016

# BASED UPON Sim_Control.pl
# Author: Lukas Swan
# Date: Oct 2009
# Copyright: Dalhousie University

# INPUT USE:
# filename.pl [house type numbers seperated by "/"] [region numbers seperated by "/"; 0 means all] [set_name]

# DESCRIPTION:
# This script generates the esp-r house files for each house of the CSDDRD.
# It uses a multithreading approach based on the house type (SD or DR) and 
# region (AT, QC, OT, PR, BC). Which types and regions are generated is 
# specified at the beginning of the script to allow for partial generation.

# The script builds a directory structure for the houses which begins with 
# the house type as top level directories, regions as second level directories 
# and the house name (10 digit w/o ".HDF") inclusing the set_name for each house directory. It places 
# all house files within that directory (all house files in the same directory). 

# The script reads a set of input files:
# 1) CSDDRD type and region database (csv)
# 2) esp-r file templates (template.xxx)
# 3) weather station cross reference list

# The script copies the template files for each house of the CSDDRD and replaces
# and inserts within the templates based on the values of the CSDDRD house. Each 
# template file is explicitly dealt with in the main code (actually a sub) and 
# utilizes insert and replace subroutines to administer the specific house 
# information.

# The script is easily extendable to addtional CSDDRD files and template files.
# Care must be taken that the appropriate lines of the template file are defined 
# and that any required changes in other template files are completed.

# ===================================================================

# --------------------------------------------------------------------
# Declare modules which are used
# --------------------------------------------------------------------

use warnings;
use strict;

use Data::Dumper;
use Parallel::ForkManager;
use XML::Simple;	# to parse the XML databases

# CHREM modules
use lib ('./modules');
use General;

#--------------------------------------------------------------------
# Declare the global variables
#--------------------------------------------------------------------
my $hse_types;	# declare an hash array to store the house types to be modeled (e.g. 1 -> 1-SD)
my $regions;	# declare an hash array to store the regions to be modeled (e.g. 1 -> 1-AT)
my $set_name;
my $InputSet;
my $cores;	# store the input core info
my @houses_desired; # declare an array to store the house names or part of to look
my @folders;	#declare an array to store the path to each hse which will be simulated
my $interval; # Number of houses to simulate on each core
my $Upgrades;   # HASH holding all the upgrade info
my $CurrentOS = $^O; # String naming the current 

my $hse_type_num;
my $region_num;
my $bRunTRNSYS = 0; # boolean to signal to run TRNSYS

#--------------------------------------------------------------------
# Read the command line input arguments
#--------------------------------------------------------------------
COMMAND_LINE: {
	if (@ARGV < 4) {die "A minimum Four arguments are required: house_types regions set_name core_information\n";};
    
	$hse_type_num = shift (@ARGV);
    $region_num = shift (@ARGV);
	# Pass the input arguments of desired house types and regions to setup the $hse_types and $regions hash references
	($hse_types, $regions, $set_name) = &hse_types_and_regions_and_set_name($hse_type_num, $region_num, shift (@ARGV));

	# Check the cores arguement which should be three numeric values seperated by a forward-slash
	unless (shift(@ARGV) =~ /^([1-9]?[0-9])\/([1-9]?[0-9])\/([1-9]?[0-9])$/) {
		die ("CORE argument requires three Positive numeric values seperated by a \"/\": #_of_cores/low_core_#/high_core_#\n");
	};
	
	# set the core information
	# 'num' is total number of cores (if only using a single QC (quad-core) then 8, if using two QCs then 16
	# 'low' is starting core, if using two QCs then the first QC has a 1 and the second QC has a 9
	# 'high' is ending core, value is 8 or 16 depending on machine
	@{$cores}{'num', 'low', 'high'} = ($1, $2, $3);
	
	# check the core infomration for validity
	unless (
		$cores->{'num'} >= 1 &&
		($cores->{'high'} - $cores->{'low'}) >= 0 &&
		($cores->{'high'} - $cores->{'low'}) <= $cores->{'num'} &&
		$cores->{'low'} >= 1 &&
		$cores->{'high'} <= $cores->{'num'}
		) {
		die ("CORE argument numeric values are inappropriate (e.g. high_core > #_of_cores)\n");
	};

	# In case no houses were provided, match everything
	@houses_desired = '.';
    
    # Update the set name
    $InputSet = $set_name;
    $set_name = "_UPG_$set_name";
};

# --------------------------------------------------------------------
# Load the upgrade inputs. If there is no upgrades, die
# --------------------------------------------------------------------
$Upgrades = XMLin("../Input_upgrade/Input_All_UPG.xml", keyattr => [], forcearray => 0);

#--------------------------------------------------------------------
# Apply the upgrades to the set
#--------------------------------------------------------------------
APPL_UP:{
    print "  =====        Calling Upgrades Script       =====\n\n";
    my @PVargs = ("Upgrade_City.pl", "$hse_type_num", "$region_num","$InputSet");
    system($^X, @PVargs);
    print "\n  =====                  Done                =====\n\n";
};

#--------------------------------------------------------------------
# Identify the house folders for simulation
#--------------------------------------------------------------------
FIND_FOLDERS: foreach my $hse_type (&array_order(values %{$hse_types})) {		#each house type
	foreach my $region (&array_order(values %{$regions})) {		#each region
		push (my @dirs, <../$hse_type$set_name/$region/*>);	#read all hse directories and store them in the array
 		#print Dumper @dirs;
		CHECK_FOLDER: foreach my $dir (@dirs) {
			# cycle through the desired house names to see if this house matches. If so continue the house build
			foreach my $desired (@houses_desired) {
				# it matches, so set the flag
				if ($dir =~ /\/$desired/) {
					push (@folders, $dir);
					next CHECK_FOLDER;
				};
			};
		};
	};
}; # END FIND_FOLDERS

#--------------------------------------------------------------------
# Determine how many houses go to each core for core usage balancing
#--------------------------------------------------------------------
$interval = int(@folders/$cores->{'num'});	#round up to the nearest integer

#--------------------------------------------------------------------
# Delete old simulation summary files
#--------------------------------------------------------------------
foreach my $file (<../summary_files/*>) { # Loop over the files
	my $check = 'Sim' . $set_name . '_';
	if ($file =~ /$check/) {unlink $file;};
};

#--------------------------------------------------------------------
# Generate and print lists of directory paths for each core to simulate
#--------------------------------------------------------------------
SIMULATION_LIST: {
	foreach my $core (1..$cores->{'num'}) {
		my $low_element = ($core - 1) * $interval;	#hse to start this particular core at
		my $high_element = $core * $interval - 1;	#hse to end this particular core at
		if ($core == $cores->{'num'}) { $high_element = $#folders};	#if the final core then adjust to end of array to account for rounding process
		my $file = '../summary_files/Sim' . $set_name . '_House-List_Core-' . $core . '.csv';
		open (HSE_LIST, '>', $file) or die ("can't open $file");	#open the file to print the list for the core
		foreach my $element ($low_element..$high_element) {
			if (defined($folders[$element])) {
				print HSE_LIST "$folders[$element]\n";	#print the hse path to the list
			};
		}
		close HSE_LIST;		#close the particular core list
	};
};

#--------------------------------------------------------------------
# Call the simulations.
#--------------------------------------------------------------------
SIMULATION: {
    print "  ===== Multithreading the ESP-r simulations =====\n\n";
    my $sCoreSim;
    # Determine which simulation core script to use based on OS
    if($CurrentOS =~ m/MSWin32/) { # Windows
        $sCoreSim = "perl Core_Sim.pl";
    } else { # linux
        $sCoreSim = "./Core_Sim.pl";
    };
    
    my $n_processes = $cores->{'num'};
    my $pm = Parallel::ForkManager->new( $n_processes );
	foreach my $core ($cores->{'low'}..$cores->{'high'}) {	#simulate the appropriate list (i.e. QC2 goes from 9 to 16)
		my $file = '../summary_files/Sim' . $set_name . '_Core-Output_Core-' . $core . '.txt';
        $pm->start and next;
		system ("$sCoreSim $core $set_name > $file");	#pass the argument $core so the program knows which set to simulate
        $pm->finish;
	}
    $pm->wait_all_children;
	print "     - THE SIMULATION OUTPUTS FOR EACH CORE ARE LOCATED IN ../summary_files/\n";
    print "\n  =====                  Done                =====\n\n";
};

#--------------------------------------------------------------------
# Aggregate the electrical and thermal demands
#--------------------------------------------------------------------
AGGREGATE: if(($Upgrades->{'DH_SYSTEM'}->{'bIsAdd'} == 1) || ($Upgrades->{'PV_ROOF'}->{'bIsAdd'} == 1)) {
     print "  =====          Aggregating the loads       =====\n\n";
     setAggregateLoads(\@folders,$set_name);
     print "\n  =====                  Done                =====\n\n";
};

#--------------------------------------------------------------------
# Post-process and save the data
#--------------------------------------------------------------------
#ESPrPost: {
#    # Run the base CHREM results
#    print "  =====    Running CHREM Results Processor   =====\n";
#    my @RESargs = ("Results.pl", "$hse_type_num", "$region_num","UPG_$InputSet", "1/1/1");
#    system($^X, @RESargs);
#    print "  =====                  Done                =====\n\n";
#};


#--------------------------------------------------------------------
# Call TRNSYS simulation (if required)
#--------------------------------------------------------------------
#TRNSYS: {
#    # Check if we need to run TRNSYS
#    if ($Upgrades->{'DH_SYSTEM'}->{'bIsAdd'} == 1) {
#        $bRunTRNSYS = 1;
#    };
#    if($bRunTRNSYS){};
#};
#--------------------------------------------------------------------
# TODO: Post-process and report performance metrics
#--------------------------------------------------------------------

#--------------------------------------------------------------------
# SUBROUTINES
#--------------------------------------------------------------------
SUBROUTINES: {
    sub setAggregateLoads{
        # INPUTS
        
        my $ref_Folder = shift;
        my $setName = shift;
        my @folders = @$ref_Folder;
        
        # INTERMEDIATES
        my @Time; # Present day of the year
        my @DHW; # Community aggregated DHW demand [W]
        my @Heat; # Community-aggregated heating demand [W]
        my @Elec; # Community-aggregated electrical demand [W] (Negative=import, Positive=export)
        my $bStoreTime=1;
        
        # Loop through each record
        foreach my $record (@folders) {
            my $hse_name = $record;
            $hse_name =~ s{.*/}{};
            
            my $sThisOut = $record . "/$hse_name.csv";
            
            # Open the timestep data
            open my $fid, $sThisOut or die "setAggregateLoads: Could not open $sThisOut\n";
            my @lines = <$fid>; # Pull entire file into an array
            close $fid;
            
            # Process the header
            my $iTime;
            my $iDHW;
            my @iHeat;
            my $iImport;
            my $iExport;
            my @Headers = split ',', $lines[0];
            for(my $i=0; $i<=$#Headers;$i++) {
                if($Headers[$i] =~ m/(present)/) {
                    $iTime = $i;
                } elsif($Headers[$i] =~ m/(water)/) {
                    $iDHW = $i;
                } elsif($Headers[$i] =~ m/(GN Heat)/) {
                    push(@iHeat,$i);
                } elsif($Headers[$i] =~ m/(net import)/) {
                    $iImport = $i;
                } elsif($Headers[$i] =~ m/(net export)/) {
                    $iExport = $i;
                };
            };
            
            # Initialize arrays (if first pass)
            if($bStoreTime) { 
                @DHW = (0) x $#lines;
                @Heat = (0) x $#lines;
                @Elec = (0) x $#lines;
            };
            
            # Loop through all the data
            for(my $i=1; $i<=$#lines;$i++) {
                my @LineData = split ',', $lines[$i];
                
                # Store the time data (if first pass)
                if($bStoreTime) {push(@Time, $LineData[$iTime]);}
                
                # Update the DHW load of the community (W)
                $DHW[$i-1] += $LineData[$iDHW];
                
                # Update community electrical consumption (W)
                $Elec[$i-1] = $Elec[$i-1] + $LineData[$iExport] - $LineData[$iImport];
                
                # Update community heating demand (W)
                my $ThisHeat = 0;
                foreach my $zone (@iHeat) {
                    $ThisHeat += $LineData[$zone];
                };
                $Heat[$i-1] += $ThisHeat;
            };
            
            $bStoreTime = 0; # Signal that at least one pass of the loop has occurred 
        };
        
        # Save the aggregated load
        PRINT_AGG: {
            my $sAggPath = "../summary_files/Aggregate_$setName.csv";
            unlink $sAggPath;
            open my $out, '>', $sAggPath or die "Can't write $sAggPath: $!";
            
            # Determine the timestep
            my $fTStep = ($Time[1]-$Time[0])*1440; # Timestep [min]
            $fTStep = sprintf "%.0f", $fTStep;
            print $out "timestep [min],$fTStep\n";
            
            # Print the headers
            print $out "present day,DHW Demand [W],Heating Demand [W],Electric Export [W]\n";
            for(my $i=0; $i<$#Time;$i++) {
                print $out "$Time[$i],$DHW[$i],$Heat[$i],$Elec[$i]\n";
            };
            close $out;
        };
        
    }; # END sub setAggregateLoads

}; # END SUBROUTINES